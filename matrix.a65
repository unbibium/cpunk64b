  processor 6502 

; This is supposed to generate a Matrix code rain effect.
;
; SYS 49152 to draw one frame.
; FOR X=1 TO 1000:SYS 49152:NEXT X to display for a minute or so

CHROUT = $FFD2
mscr = $FB
mcolor = $FD


; color constants
WHITE = 1
LT_GREEN = 13
DK_GREEN = 5
BLACK = 0

  IFCONST VIC20
WIDTH = 22
HEIGHT = 23
; unexpanded vic
SCRMEM = 7680
COLMEM = 38400
PAGES = 2
    ORG $33c

  ELSE
; screen dimensions
WIDTH = 40
HEIGHT = 25
SCRMEM = 1024
COLMEM = 55296
PAGES = 4

    ORG $c000

  ENDIF
   

INIT:
; turn screen black and clear
  IFCONST VIC20
    lda #8
    sta 36879
  ELSE
    lda #0
    sta $d020
    sta $d021
  ENDIF
; main loop
LOOP
    ldy #WIDTH-1
LOOP1:
    lda raindrops,y ;get raindrop position
    ; reset if >64
    bmi LOOPA
    cmp #HEIGHT+numcolors
    bmi LOOPA
    ; get random number -16 <= n <= -1
    jsr GETRAND
    ora #$F0
    sta raindrops,y
LOOPA:
    ;cmp #HEIGHT-1
    ;bcs LOOP2
    jsr MULROW
    ; see if (mscr),y falls within screen boundary
    ldx mscr+1
    tya
    clc
    adc mscr
    bcc LOOPB
    inx
LOOPB:
    cpx #>SCRMEM
    bmi DRAWTRAIL ; lower bound check
    cpx #>SCRMEM+PAGES
    bpl DRAWTRAIL
    jsr GETRAND
    sta (mscr),y
DRAWTRAIL:
    ; draw color trail
    ldx #numcolors-1 ; color index
TRAILLOOP:
    ; see if (mcolor),y falls within screen boundary
    tya
    clc
    adc mcolor
    lda #0
    adc mcolor+1
    cmp #>COLMEM
    bmi NEXTTRAIL
    cmp #>COLMEM+PAGES
    bpl NEXTTRAIL
    lda COLORS,x
    sta (mcolor),y
NEXTTRAIL:
    lda mcolor
    sec
    sbc #WIDTH
    sta mcolor
    bcs NEXTTRAIL2
    dec mcolor+1
NEXTTRAIL2:
    dex
    bpl TRAILLOOP
    
LOOP2
    tya
    tax
    inc raindrops,x
    dey
    bpl LOOP1
ENDUT: 
    rts

;
; Multiply a row number by 40
;
MULROW:
    ; multiply by five
    sta mscr
    ldx #0
    stx mscr+1
    asl
    rol mscr+1
    asl
    rol mscr+1
    adc mscr
    bcc MULROWB
    inc mscr+1
MULROWB:
    ; multiply by eight
    rol 
    rol mscr+1
    rol
    rol mscr+1
    rol
    rol mscr+1
    ; write low byte
    sta mscr
    sta mcolor
    ; add 4 for screen location
    lda mscr+1
    clc
    adc #>SCRMEM
    sta mscr+1
    ; add more for color location
    adc #>COLMEM->SCRMEM
    sta mcolor+1
    rts

; use ROM as a substitute for random bytes

ROMRAND = $e000

GETRAND:
    inc RANDPTR
    bne GETRAND2
    inc RANDPTR+1
    bne GETRAND2
    lda #>ROMRAND
    sta RANDPTR+1
GETRAND2:
    lda ROMRAND
    rts

RANDPTR = GETRAND2+1

COLORS:
    byte BLACK,DK_GREEN,BLACK,DK_GREEN
    byte DK_GREEN,LT_GREEN,DK_GREEN,LT_GREEN,LT_GREEN,WHITE
ENDCOLORS

numcolors = ENDCOLORS-COLORS

; TODO: randomize
; one raindrop for each column on the screen
raindrops: 
    byte 3,9,13,7,10,8,4,6
    byte 2,9,3,1,12,10,9,4
    byte 3,9,12,5,13,1,4,6
    byte 2,9,3,1,12,10,9,4
    byte 3,9,14,8,10,1,4,6

